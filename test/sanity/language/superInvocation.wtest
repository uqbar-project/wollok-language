/**
  * Scenario: a hierarchy of classes having a method that 
  * uses super invocation
  */
class Golondrina {
  var energia = 100
  method energia() {
    return energia
  }

  method volar(kms) {
    energia = energia - self.gastoParaVolar(kms)
  }

  method gastoParaVolar(kms) {
    return kms
  }

  method comer() {
    energia = energia + 10
  }
}

class SeCansaMucho inherits Golondrina {
  override method gastoParaVolar(kms) {
    return 2 * super(kms)
  }

  override method comer() {
    2.times { vez => super() }
  }
}

/**
  * Tests
  */
test "super invocation" {
  const pepona = new SeCansaMucho()
  pepona.volar(50)
  assert.equals(0, pepona.energia())
}

test "super invocation inside closure" {
  const pepona = new SeCansaMucho()
  pepona.comer()
  assert.equals(120, pepona.energia())
}
