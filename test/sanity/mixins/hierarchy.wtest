/**
  * Scenario: a named object inheriting from both superclasses and mixins
  */
mixin M {
  method hierarchy() = "M <: " + super()
}

mixin N {
  method hierarchy() =  "N <: " +  super()
}

mixin O {
  method hierarchy() =  "O <: " +  super()
}

object a inherits M and N and B {
    method hierarchy() = "a <: " + super()  
}

class B inherits O and C {
    method hierarchy() =  "B <: " + super()  
}

class C {
    method hierarchy() = "C"
}

test "linearization from a named object with mixins and classes" {
  assert.equals("a <: M <: N <: B <: O <: C", a.hierarchy())
}

mixin M0 { method hierarchy() = "M0" }
mixin M1 inherits M0 { method hierarchy() = "M1 <: " + super() }
mixin M2 inherits M0 { method hierarchy() = "M2 <: " + super() }

test "linearization of common mixin ancestors should be pushed backwards to enable cake pattern default" {
  assert.equals("M0", object inherits M0 {}.hierarchy())
  assert.equals("M1 <: M0", object inherits M1 {}.hierarchy())
  assert.equals("M2 <: M0", object inherits M2 {}.hierarchy())
  assert.equals("M1 <: M2 <: M0", object inherits M1 and M2 {}.hierarchy())
  assert.equals("M2 <: M1 <: M0", object inherits M2 and M1 {}.hierarchy())
}
