/**
  * Scenario: a class and several mixins they will be applied
  * in different ways
  */
class Warrior {}

mixin Energy {
  var energy = 100
  method energy() = energy

  method a (e) { energy -= e }
}

mixin Energy2 {
  var energy = 100
  method energy() = energy
  method energy(e) { energy = e }
}

mixin GetsHurt {
  method receiveDamage(amount) {
    self.energy(self.energy() - amount)
  }
  method energy()
  method energy(newEnergy)
}

mixin Attacks {
  var power = 10
  method attack(other) {
    other.receiveDamage(power)
    self.energy(self.energy() - 1)
  }
  method power() = power
  method power(p) { power = p }

  method energy()
  method energy(newEnergy)
}

test "single mixin at instantiation time" {
  const warrior = object inherits Energy and Warrior { }
  assert.equals(100, warrior.energy())
}

test "multiple mixins at instantiation time" {
  const warrior1 = object inherits Attacks and Energy2 and GetsHurt and Warrior { }
  assert.equals(100, warrior1.energy())

  const warrior2 = object inherits Attacks and Energy2 and GetsHurt and Warrior { }
  assert.equals(100, warrior2.energy())

  warrior1.attack(warrior2)

  assert.equals(90, warrior2.energy())
  assert.equals(99, warrior1.energy())
}
